# case insensitive autocomplete
zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}'

# red dots to be displayed while waiting for completion
COMPLETION_WAITING_DOTS="true"

# load our own completion functions
fpath=(~/.zsh/completion /usr/local/share/zsh/site-functions $fpath)

# append completion cache directory (generated by task completions:generate)
fpath=(~/.cache/zsh/completions $fpath)

# append asdf completions to fpath
fpath=(${ASDF_DIR}/completions $fpath)

# append rust/cargo completions from asdf installation
if [[ -d "${ASDF_DIR}/installs/rust" ]]; then
  # Find the current rust version's completion directory
  rust_comp_dir=$(find "${ASDF_DIR}/installs/rust" -path "*/share/zsh/site-functions" -type d | head -1)
  [[ -n "$rust_comp_dir" ]] && fpath=($rust_comp_dir $fpath)
fi

# append homebrew completions to fpath
fpath=(/opt/homebrew/share/zsh/site-functions $fpath)

# completion only refresh once a day
# autoload -U compinit
# compinit
autoload -Uz +X bashcompinit && bashcompinit
autoload -Uz +X compinit

for dump in ~/.zcompdump(N.mh+24); do
  compinit
done

compinit -C

# Explicitly load task completion (autoload from fpath doesn't always work)
if [[ -f ~/.cache/zsh/completions/_task ]]; then
  source ~/.cache/zsh/completions/_task
fi

# Override slow default source completion
# Default searches all files in $PATH which causes hangs
# This limits to current dir, common config locations, and recently used
_source_custom() {
  if [[ CURRENT -ge 3 ]]; then
    compset -n 2
    _normal
  else
    # Only search in likely locations for source files, not entire $PATH
    _files -W "(. ~/.config/zsh ~/.local ~/.dotfiles)"
  fi
}

compdef _source_custom source
compdef _source_custom .

# Mix task completion (from OMZ mix-fast plugin)
# Defined here instead of loading as zinit snippet to avoid compdef timing issues
_mix_refresh () {
  if [ -f .mix_tasks ]; then
    rm .mix_tasks
  fi
  echo "Generating .mix_tasks..." > /dev/stderr
  _mix_generate
  cat .mix_tasks
}

_mix_does_task_list_need_generating () {
  [ ! -f .mix_tasks ];
}

_mix_generate () {
  mix help | grep '^mix [^ ]' | sed -E "s/mix ([^ ]*) *# (.*)/\1:\2/" > .mix_tasks
}

_mix () {
  if [ -f mix.exs ]; then
    if _mix_does_task_list_need_generating; then
      echo "\nGenerating .mix_tasks..." > /dev/stderr
      _mix_generate
    fi
    local tasks=(${(f)"$(cat .mix_tasks)"})
    _describe 'tasks' tasks
  fi
}

compdef _mix mix
alias mix_refresh='_mix_refresh'

# Optional: Enable completion profiling to find slow completions
# Uncomment to see timing for completions >50ms
# source ~/.dotfiles/scripts/profile-completion.zsh

